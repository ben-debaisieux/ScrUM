#To edit and compare internal_properties, use WINDEV integrated tools.
#Internal properties refer to the properties of controls in windows, reports, etc.
info :
 name : COL_PLANNER
 major_version : 27
 minor_version : 0
 type : 7
 description : ""
 subtype : 0
procedure_set :
 identifier : 0x155db8b400319896
 internal_properties : BwAAAAcAAABGLu41kG7fjQV3iS4F72qmnKaNh5694reolNKIW0iw
 code_elements :
  type_code : 31
  p_codes :
   -
     code : |1+
      STR_SHAREPOINT_DOC est une Structure
      	sNom			est une chaîne
      	sChemin_Complet	est une chaine
      FIN
      
      STR_PLANNER_Task est une Structure
      	sIDTask			est une chaine
      	sNom_Task		est une chaine
      	sIDBucket		est une chaine
      	sIDPlan			est une chaine
      	nPriorité		est un entier
      	sDescription	est une chaine
      	sEtag			est une chaine
      	astDocument		est un tableau de STR_SHAREPOINT_DOC
      	aStEtiquette est un tableau associatif de STR_PLANNER_ETIQUETTE
      FIN
      
      STR_PLANNER_Bucket est une structure
      	sNom_Bucket	est une chaîne
      	sIDBucket	est une chaine
      	
      	aStTask		est un tableau de STR_PLANNER_Task
      FIN
      
      STR_PLANNER est une Structure
      	sIDPlanner	est une chaine
      	sNom_Planer	est une chaine
      	sIDGroupe	est une chaine
      	aStEtiquette est un tableau associatif de STR_PLANNER_ETIQUETTE
      	
      	aStBucket	est un tableau associatif de STR_PLANNER_Bucket
      FIN
      
      STR_PLANNER_ETIQUETTE est une structure
      	sIDCategory est une chaine
      	sNom_Category est une chaine
      	sValeur est une chaine
      FIN
      
      STR_PLANNER_PROJET est une structure
      	sIDPlanner est une chaine
      	sIDBucket_A_Faire est une chaine
      	sIDBucket_En_Cours est une chaine
      	sIDBucket_A_Tester est une chaine
      	sIDBucket_A_Livrer est une chaine
      fin
      
      CONSTANTE
      	cstPlanner_Bucket_A_Faire = "à faire"
      	cstPlanner_Bucket_En_Cours = "en cours"
      	cstPlanner_Bucket_A_Tester = "à tester"
      	cstPlanner_Bucket_A_Livrer = "à livrer"
      FIN
     type : 720896
  procedures :
   -
     name : PLANNER_Construction_Execution_Requete
     procedure_id : 1539589958500771586
     type_code : 15
     code : |1+
      PROCÉDURE prive PLANNER_Construction_Execution_Requete(LOCAL sURL_p est une chaîne, LOCAL nMéthode_p est un entier, LOCAL sContenu_p est une chaîne = "",LOCAL sEtag_Entete_p est une chaine = "")<métier>:restRéponse
      
      // variables
      rest_Requete	est un restRequête
      rest_Reponse	est un restRéponse
      stoken			est une chaîne
      
      
      // récupération token
      stoken = COL_PLANNER.PLANNER_Generation_Token()
      
      // entete
      rest_Requete..Entête["Authorization"]	= "bearer " + stoken
      
      SI sEtag_Entete_p <> "" ALORS
      	rest_Requete..Entête["If-Match"]	= sEtag_Entete_p
      FIN
      
      // url de la requête
      rest_Requete..URL						= Encode(sURL_p,encodeURLDepuisUnicode)
      
      rest_Requete..Méthode					= nMéthode_p
      
      rest_Requete..ContentType				= "application/json"
      
      
      SI sContenu_p <> "" //_ET_ JSONValide(sContenu_p) ALORS
      	// contenu de la requete au format JSON
      	rest_Requete..Contenu					= ChaîneVersJSON(sContenu_p)
      	
      FIN
      
      // envoi de la requête
      rest_Reponse							= RESTEnvoie(rest_Requete)
      
      
      RENVOYER rest_Reponse
      
     type : 458752
   -
     name : PLANNER_Création_Tache
     procedure_id : 1539589997155552161
     type_code : 15
     code : |1-
      procédure PLANNER_Création_Tache(LOCAL stTask_p	est un STR_PLANNER_Task):booléen
      
      
      // variables
      rest_Reponse		est un restRéponse
      sContenu			est une chaine
      bRetour				est un booléen
      sDocument_Temp		est une chaine
      sDocument			est une chaine
      vRetour				est un variant
      sIDTache est une chaine
      
      // encode chaine en JSON
      stTask_p.sNom_Task		= Encode_Chaine_JSON(stTask_p.sNom_Task)
      stTask_p.sDescription	= Encode_Chaine_JSON(stTask_p.sDescription)
      
      SI TableauOccurrence(stTask_p.astDocument) > 0 ALORS
      	POUR nLigne =  1 _À_ TableauOccurrence(stTask_p.astDocument)
      		sDocument_Temp += ["," + RC] + [
      	{
      	"[%stTask_p.astDocument[nLigne].sChemin_Complet%]":{
            "@odata.type": "microsoft.graph.plannerExternalReference",
            "alias": "[%stTask_p.astDocument[nLigne].sNom%]",
            "previewPriority": " !",
            "type": "Other"
      	}
      	}
      		]
      	FIN	
      	sDocument = [
      		,
      		"references": [%sDocument_Temp%]
      		
      	]
      FIN
      
      
      // structure JSON création tache
      sContenu = [
      	{
      	"planId": "[%stTask_p.sIDPlan%]",
      	"bucketId": "[%stTask_p.sIDBucket%]",
      	"title": "[%stTask_p.sNom_Task%]",
      	"description": "[%stTask_p.sDescription%]"
      }
      ]
      
      // création task
      rest_Reponse	= COL_PLANNER.PLANNER_Construction_Execution_Requete("https://graph.microsoft.com/v1.0/planner/tasks",httppost,sContenu)
      
      SI rest_Reponse..CodeEtat DANS (200,201,202) ALORS
      	// création task ok !
      	bRetour = vrai
      	
      	// on est obligé de modifier la tache après l'avoir créé car tous les éléments ne sont pas présent lors de la création de la tache
      	
      	vRetour = JSONVersVariant(rest_Reponse..Contenu)
      	
      	// id de la tache
      	sIDTache = vRetour.id
      	
      	stTask_p.sIDTask = sIDTache
      	
      	bRetour = COL_PLANNER.PLANNER_Modification_Tache(stTask_p)
      	
      SINON
      	// requête hs
      	bRetour = faux
      FIN
      
      RENVOYER bRetour
     type : 458752
   -
     name : PLANNER_Generation_Token
     procedure_id : 1539590027220396144
     type_code : 15
     code : |1+
      procédure PLANNER_Generation_Token():chaine
      
      // variables
      http_Requete		est un httpRequête
      http_Reponse		est un httpRéponse
      vRetour				est un Variant
      sToken				est une chaîne
      sIDClient			est une chaîne
      sIDClient_Secret	est une chaîne
      sLocataire			est une chaîne
      
      sIDClient			= "93a85105-3544-4cdf-851b-389fe72f1701"
      sIDClient_Secret	= "CEM8Q~omxlvaDcRmtJGxK1dQCa8HFnRmaQRKhc9_"
      sLocataire			= "4cfca23f-1dd9-4197-91b5-5063ebc48413"
      
      
      // formulaire http
      HTTPCréeFormulaire("Token")
      HTTPAjouteParamètre("Token","client_id",sIDClient)
      HTTPAjouteParamètre("Token","scope",URLEncode("https://graph.microsoft.com/.default"))
      HTTPAjouteParamètre("Token","client_secret",sIDClient_Secret)
      HTTPAjouteParamètre("Token","grant_type","client_credentials")
      
      // url token
      http_Requete..URL			= "https://login.microsoftonline.com/[%sLocataire%]/oauth2/v2.0/token"
      
      http_Requete..Méthode		= httpPost
      
      http_Requete..ContentType	= "application/x-www-form-urlencoded"
      
      
      
      http_Reponse				= HTTPEnvoieFormulaire("token",http_Requete)
      
      SI http_Reponse..CodeEtat PAS DANS (200,201,202) ALORS
      	
      SINON
      	// variant contenant valeur de retour
      	vRetour	= JSONVersVariant(http_Reponse..Contenu)
      	
      	// récupération du token
      	sToken	= vRetour.access_token
      FIN
      
      // renvoi le token
      RENVOYER sToken
     type : 458752
   -
     name : PLANNER_ID_GROUPES
     procedure_id : 1539590061580207204
     type_code : 15
     code : |1+
      PROCÉDURE PLANNER_ID_GROUPES(LOCAL sNomGroupes est une chaîne)<métier>:chaîne
      
      sIDGroupes		est une chaîne
      rest_Reponse	est une restRéponse
      vRetour			est un Variant
      
      rest_Reponse = PLANNER_Construction_Execution_Requete("https://graph.microsoft.com/v1.0/groups?$filter=displayName eq '[%sNomGroupes%]'",httpGet)
      
      SI rest_Reponse..CodeEtat DANS (200,201,202) ALORS
      	//SI JSONValide(rest_Reponse..Contenu) ALORS
      		vRetour = JSONVersVariant(rest_Reponse..Contenu)
      		
      		SI TableauOccurrence(vRetour.value) = 1 ALORS
      			sIDGroupes = vRetour.value[1].id
      		FIN
      	//FIN
      FIN
      
      RENVOYER sIDGroupes
     type : 458752
   -
     name : PLANNER_Récupération_Liste_Bucket_Taches
     procedure_id : 1539590095940019483
     type_code : 15
     code : |1-
      
      procédure PLANNER_Récupération_Liste_Bucket_Taches(LOCAL stPlanner_p est un STR_PLANNER):tableau associatif de STR_PLANNER_Bucket
      
      // variables
      rest_Reponse		est un restRéponse
      vRetour				est un variant
      stPlanner_Task		est un STR_PLANNER_Task
      stPlanner_Bucket	est un STR_PLANNER_Bucket
      aStPlanner_Bucket	est un tableau associatif de STR_PLANNER_Bucket
      sIDBucket			est une chaine
      stEtiquette		est un STR_PLANNER_ETIQUETTE
      sIDCategory est une chaine
      
      // récupération des plans du groupe
      rest_Reponse	= COL_PLANNER.PLANNER_Construction_Execution_Requete("https://graph.microsoft.com/v1.0/planner/plans/[%stPlanner_p.sIDPlanner%]/tasks",httpGet)
      
      SI rest_Reponse..CodeEtat DANS (200,201,202) ALORS
      	// requête ok
      	vRetour = JSONVersVariant(rest_Reponse..Contenu)
      	POUR nLigne = 1 _À_ TableauOccurrence(vRetour.value)
      		
      		// vide toute la structure
      		VariableRAZ(stPlanner_Task)
      		
      		sIDBucket = vRetour.value[nLigne].bucketId
      		
      		SI aStPlanner_Bucket[sIDBucket]..Vide ALORS
      			aStPlanner_Bucket[sIDBucket]				= allouer un STR_PLANNER_Bucket
      			
      			stPlanner_Bucket							= COL_PLANNER.PLANNER_stBucket(sIDBucket)
      			
      			aStPlanner_Bucket[sIDBucket].sNom_Bucket	= stPlanner_Bucket.sNom_Bucket
      			aStPlanner_Bucket[sIDBucket].sIDBucket		= stPlanner_Bucket.sIDBucket
      		FIN
      		
      		stPlanner_Task				= COL_PLANNER.PLANNER_stTask(vRetour.value[nLigne].id)
      		
      		stPlanner_Task.sIDPlan		= stPlanner_p.sIDPlanner
      		stPlanner_Task.sIDBucket	= vRetour.value[nLigne].bucketId
      		stPlanner_Task.sIDTask		= vRetour.value[nLigne].id
      		stPlanner_Task.sNom_Task	= vRetour.value[nLigne].title
      		stPlanner_Task.nPriorité	= vRetour.value[nLigne].priority	
      		
      		// tableau des étiquette relié à la tache 
      		POUR i = 1 _À_ vRetour.value[nLigne].appliedCategories..Membre..Occurrence
      			// on vite la structure des étiquettes
      			VariableRAZ(stEtiquette)
      			
      			sIDCategory = vRetour.value[nLigne].appliedCategories..Membre[i]..Nom
      			
      			SI stPlanner_p.aStEtiquette[sIDCategory]..Existe ALORS
      				stEtiquette.sIDCategory		= stPlanner_p.aStEtiquette[sIDCategory].sIDCategory
      				stEtiquette.sNom_Category	= stPlanner_p.aStEtiquette[sIDCategory].sNom_Category
      			SINON
      				stEtiquette.sIDCategory		= sIDCategory
      			FIN
      			
      			
      			SI PAS stPlanner_Task.aStEtiquette[sIDCategory]..Existe ALORS
      				stPlanner_Task.aStEtiquette[sIDCategory]	= allouer un STR_PLANNER_ETIQUETTE
      				stPlanner_Task.aStEtiquette[sIDCategory]	= stEtiquette 
      			FIN
      			
      		FIN	
      		
      		// ajoute la tache dans le bucket planner
      		TableauAjoute(aStPlanner_Bucket[sIDBucket].aStTask,stPlanner_Task)
      	FIN
      SINON
      	// requête hs
      	
      FIN
      
      RENVOYER aStPlanner_Bucket
     type : 458752
   -
     name : PLANNER_Récupération_Liste_Planner_Par_Groupe
     procedure_id : 1539590126004798071
     type_code : 15
     code : |1+
      procédure PLANNER_Récupération_Liste_Planner_Par_Groupe(LOCAL sNomGroupe_p est une chaîne)<metier>:tableau associatif de STR_PLANNER
      
      // variables
      rest_Reponse	est un restRéponse
      sIDGroupe		est une chaine
      vRetour			est un variant
      aStPlanner		est un tableau associatif de STR_PLANNER
      stPlanner		est un STR_PLANNER
      
      // récupération ID Groupes
      sIDGroupe		= COL_PLANNER.PLANNER_ID_GROUPES(sNomGroupe_p)
      
      // récupération des plans du groupe
      rest_Reponse	= COL_PLANNER.PLANNER_Construction_Execution_Requete("https://graph.microsoft.com/v1.0/groups/[%sIDGroupe%]/planner/plans",httpGet)
      
      SI rest_Reponse..CodeEtat DANS (200,201,202) ALORS
      	// requête ok
      	vRetour = JSONVersVariant(rest_Reponse..Contenu)
      	POUR nLigne = 1 _À_ TableauOccurrence(vRetour.value)
      		stPlanner.sNom_Planer	= vRetour.value[nLigne].title
      		stPlanner.sIDPlanner	= vRetour.value[nLigne].id
      		stPlanner.sIDGroupe		= vRetour.value[nLigne].owner
      		
      		stPlanner.aStEtiquette	= COL_PLANNER.PLANNER_stListe_Etiquettes_Plan(stPlanner.sIDPlanner)
      		stPlanner.aStBucket		= COL_PLANNER.PLANNER_Récupération_Liste_Bucket_Taches(stPlanner)
      		
      		SI PAS aStPlanner[stPlanner.sIDPlanner]..existe ALORS
      			aStPlanner[stPlanner.sIDPlanner] = allouer un STR_PLANNER
      			aStPlanner[stPlanner.sIDPlanner] = stPlanner
      		FIN
      	FIN
      	
      	
      SINON
      	// requête hs
      	
      FIN
      
      RENVOYER aStPlanner
      
     type : 458752
   -
     name : PLANNER_stBucket
     procedure_id : 1539590156069641522
     type_code : 15
     code : |1+
      procédure PLANNER_stBucket(LOCAL sIDBucket_p est une chaine):STR_PLANNER_Bucket
      
      // https://graph.microsoft.com/v1.0/planner/buckets/hsOf2dhOJkqyYYZEtdzDe2QAIUCR
      
      // variables
      rest_Reponse		est un restRéponse
      vRetour				est un variant
      stPlanner_Bucket	est un STR_PLANNER_Bucket
      
      // récupération des plans du groupe
      rest_Reponse	= COL_PLANNER.PLANNER_Construction_Execution_Requete("https://graph.microsoft.com/v1.0/planner/buckets/[%sIDBucket_p%]",httpGet)
      
      SI rest_Reponse..CodeEtat DANS (200,201,202) ALORS
      	// requête ok
      	vRetour = JSONVersVariant(rest_Reponse..Contenu)
      	
      	stPlanner_Bucket.sIDBucket = vRetour.id
      	stPlanner_Bucket.sNom_Bucket = vRetour.name
      	
      SINON
      	// requête hs
      	
      FIN
      
      renvoyer stPlanner_Bucket
     type : 458752
   -
     name : PLANNER_stTask
     procedure_id : 1539590181839516350
     type_code : 15
     code : |1-
      procédure PLANNER_stTask(LOCAL sIDTask_p est une chaine):STR_PLANNER_Task
      
      // variables
      rest_Reponse	est un restRéponse
      vRetour			est un variant
      stTask est un STR_PLANNER_Task
      stDocument est un STR_SHAREPOINT_DOC
      
      // récupération des plans du groupe
      rest_Reponse	= COL_PLANNER.PLANNER_Construction_Execution_Requete("https://graph.microsoft.com/beta/planner/tasks/[%sIDTask_p%]/details",httpGet)
      
      SI rest_Reponse..CodeEtat DANS (200,201,202) ALORS
      	// requête ok
      	vRetour = JSONVersVariant(rest_Reponse..Contenu)
      	
      	stTask.sDescription = vRetour.description
      	stTask.sEtag = vRetour."@odata.etag"
      	
      	POUR TOUT stReference,sChemin_doc DE vRetour.references
      		stDocument.sNom = stReference.alias
      		stDocument.sChemin_Complet = URLDécode(sChemin_doc)
      		TableauAjoute(stTask.astDocument,stDocument)
      	FIN
      SINON
      	// requête hs
      	
      FIN
      
      RENVOYER stTask
     type : 458752
   -
     name : TEAMS_Récupération_Document
     procedure_id : 1539590207609391803
     type_code : 15
     code : |1+
      procédure TEAMS_Récupération_Document(LOCAL sChemin_fichier_p est une chaine, LOCAL sGroupe_ID_p est une chaine = "dba2d53d-84c3-4334-bd15-a9159bbd5ce1")
      
      rest_Reponse	est un restRéponse
      
      // channels
      rest_Reponse = COL_PLANNER.PLANNER_Construction_Execution_Requete("https://graph.microsoft.com/v1.0/teams/[%sGroupe_ID_p%]/channels",httpGet)
      
      // sites
      rest_Reponse = COL_PLANNER.PLANNER_Construction_Execution_Requete("https://graph.microsoft.com/v1.0/groups/[%sGroupe_ID_p%]/sites",httpGet)
      
      COL_PLANNER.PLANNER_Construction_Execution_Requete("https://graph.microsoft.com/v1.0/teams/[%sGroupe_ID_p%]/channels/{channel_id}/files/{file_name}/content",httpPut)
      
      RENVOYER sChemin_fichier_p
      
      
      
     type : 458752
   -
     name : PLANNER_stListe_Etiquettes_Plan
     procedure_id : 1550382369559312578
     type_code : 15
     code : |1+
      // Résumé : <indiquez ici ce que fait la procédure>
      // Syntaxe :
      //[ <Résultat> = ] PLANNER_stListe_Etiquettes_Plan (<sIDPlan_p> est chaîne)
      //
      // Paramètres :
      //	sIDPlan_p (chaîne ANSI) : <indiquez ici le rôle de sIDPlan_p>
      // Valeur de retour :
      // 	tableau associatif (STR_PLANNER_ETIQUETTE) : <indiquez ici le rôle de la valeur de retour>
      //
      // Exemple :
      // <Indiquez ici un exemple d'utilisation>
      //
      procédure PLANNER_stListe_Etiquettes_Plan(LOCAL sIDPlan_p est une chaine)<metier>:tableau associatif de STR_PLANNER_ETIQUETTE
      
      // variables
      rest_Reponse	est un restRéponse
      vRetour			est un Variant
      aStEtiquette	est un tableau associatif de STR_PLANNER_ETIQUETTE
      stEtiquette		est un STR_PLANNER_ETIQUETTE
      
      // récupération des plans du groupe
      rest_Reponse	= COL_PLANNER.PLANNER_Construction_Execution_Requete("https://graph.microsoft.com/beta/planner/plans/[%sIDPlan_p%]/details",httpGet)
      
      SI rest_Reponse..CodeEtat DANS (200,201,202) ALORS
      	// requête ok
      	vRetour				= JSONVersVariant(rest_Reponse..Contenu)
      	
      	POUR i = 1 _À_ vRetour.categoryDescriptions..Membre..Occurrence
      		
      		stEtiquette.sIDCategory		= vRetour.categoryDescriptions..Membre[i]..Nom
      		stEtiquette.sNom_Category	= vRetour.categoryDescriptions..Membre[i]
      		
      		SI stEtiquette.sNom_Category <> "0" ALORS
      			// ne rien faire
      		SINON
      			stEtiquette.sNom_Category = ""
      		FIN
      		SI PAS aStEtiquette[stEtiquette.sIDCategory]..Existe ALORS
      			aStEtiquette[stEtiquette.sIDCategory]	= allouer un STR_PLANNER_ETIQUETTE
      			aStEtiquette[stEtiquette.sIDCategory]	= stEtiquette
      		FIN
      	FIN
      SINON
      	// requête hs
      	
      FIN
      
      RENVOYER aStEtiquette
      
     type : 458752
   -
     name : PLANNER_SCRYNCRONISATION
     procedure_id : 1575967171920638290
     type_code : 15
     code : |1+
      // Résumé : <indiquez ici ce que fait la procédure>
      // Syntaxe :
      // PLANNER_SCRYNCRONISATION ()
      //
      // Paramètres :
      //	Aucun
      // Valeur de retour :
      // 	Aucune
      //
      // Exemple :
      // <Indiquez ici un exemple d'utilisation>
      //
      procédure PLANNER_SCRYNCRONISATION()
      
      
     type : 458752
   -
     name : PLANNER_LISTE_BUCKET
     procedure_id : 1575973665912046123
     type_code : 15
     code : |1-
      // Résumé : <indiquez ici ce que fait la procédure>
      // Syntaxe :
      //[ <Résultat> = ] PLANNER_LISTE_BUCKET (<sIDPlan_p> est chaîne [, <bPar_Nom_p> est booléen])
      //
      // Paramètres :
      //	sIDPlan_p (chaîne ANSI) : <indiquez ici le rôle de sIDPlan_p>
      //	bPar_Nom_p (booléen - valeur par défaut=0) : <indiquez ici le rôle de bPar_Nom_p>
      
      // Valeur de retour :
      // 	tableau associatif (STR_PLANNER_Bucket) : <indiquez ici le rôle de la valeur de retour>
      //
      // Exemple :
      // <Indiquez ici un exemple d'utilisation>
      //
      procédure PLANNER_LISTE_BUCKET(LOCAL sIDPlan_p est une chaine, LOCAL bPar_Nom_p est un booleen = faux)<metier>:tableau associatif de STR_PLANNER_Bucket
      
      
      // https://graph.microsoft.com/v1.0/planner/plans/{plan_id}/buckets
      
      // variables
      rest_Reponse	est un restRéponse
      vRetour			est un Variant
      aStBucket est un tableau associatif de STR_PLANNER_Bucket
      stBucket est un STR_PLANNER_Bucket
      
      // récupération des plans du groupe
      rest_Reponse	= COL_PLANNER.PLANNER_Construction_Execution_Requete("https://graph.microsoft.com/beta/planner/plans/[%sIDPlan_p%]/buckets",httpGet)
      
      SI rest_Reponse..CodeEtat DANS (200,201,202) ALORS
      	// requête ok
      	vRetour				= JSONVersVariant(rest_Reponse..Contenu)
      	
      	POUR i = 1 _À_ TableauOccurrence(vRetour.value)
      		VariableRAZ(stBucket)
      		stBucket.sIDBucket = vRetour.value[i].id
      		stBucket.sNom_Bucket = vRetour.value[i].name
      		
      		SI bPar_Nom_p = Vrai ALORS
      			SI PAS aStBucket[stBucket.sNom_Bucket]..Existe ALORS
      				aStBucket[stBucket.sNom_Bucket]	= allouer un STR_PLANNER_Bucket
      				aStBucket[stBucket.sNom_Bucket]	= stBucket
      			FIN
      		SINON
      			SI PAS aStBucket[stBucket.sIDBucket]..Existe ALORS
      				aStBucket[stBucket.sIDBucket]	= allouer un STR_PLANNER_Bucket
      				aStBucket[stBucket.sIDBucket]	= stBucket
      			FIN
      		FIN
      	FIN
      SINON
      	// requête hs
      	
      FIN
      
      RENVOYER aStBucket
     type : 458752
   -
     name : PLANNER_Modification_Tache
     procedure_id : 1576037661005258262
     type_code : 15
     code : |1-
      procédure PLANNER_Modification_Tache(LOCAL stTask_p	est un STR_PLANNER_Task):booléen
      
      
      // variables
      rest_Reponse		est un restRéponse
      sContenu			est une chaine
      bRetour				est un booléen
      sCategory est une chaine
      sEtag est une chaine
      vRetour est un variant
      
      
      // récupération JSON étiquette
      POUR TOUT stEtiquette DE stTask_p.aStEtiquette
      	sCategory += ["," + RC] + [
      	"[%stEtiquette.sIDCategory%]": [%stEtiquette.sValeur%]
      	]
      FIN
      
      // encode chaine en JSON
      stTask_p.sNom_Task = Encode_Chaine_JSON(stTask_p.sNom_Task)
      stTask_p.sDescription = Encode_Chaine_JSON(stTask_p.sDescription)
      
      sContenu = [
       	{
       		"title": "[%stTask_p.sNom_Task%]",
       		"description": "[%stTask_p.sDescription%]",
       		"appliedCategories": {
        			[%sCategory%]
        		}
      	} 	
      ]
      
      // premiere requette GET pour récupérer l'etag de la tache
      rest_Reponse	= COL_PLANNER.PLANNER_Construction_Execution_Requete("https://graph.microsoft.com/v1.0/planner/tasks/[%stTask_p.sIDTask%]",httpGet)
      SI rest_Reponse..CodeEtat DANS (200,201,202) ALORS
      	vRetour	= JSONVersVariant(rest_Reponse..Contenu)
      	
      	sEtag			= vRetour."@odata.etag"
      	
      	// deuxième requette modifie la tache
      	rest_Reponse	= COL_PLANNER.PLANNER_Construction_Execution_Requete("https://graph.microsoft.com/v1.0/planner/tasks/[%stTask_p.sIDTask%]",httpPatch,sContenu,sEtag)
      	
      	SI rest_Reponse..CodeEtat DANS (200,201,202,204) ALORS
      		sContenu = [
       	{
       		"description": "[%stTask_p.sDescription%]"
      	} 	
      		]
      		
      		// troisième requette récupère etag du detail de la tache
      		rest_Reponse	= COL_PLANNER.PLANNER_Construction_Execution_Requete("https://graph.microsoft.com/v1.0/planner/tasks/[%stTask_p.sIDTask%]/details",httpGet)
      		
      		SI rest_Reponse..CodeEtat DANS (200,201,202) ALORS
      			vRetour			= JSONVersVariant(rest_Reponse..Contenu)
      			
      			sEtag			= vRetour."@odata.etag"
      			
      			// troisième requette modifie les détails de la tache
      			rest_Reponse	= COL_PLANNER.PLANNER_Construction_Execution_Requete("https://graph.microsoft.com/v1.0/planner/tasks/[%stTask_p.sIDTask%]/details",httpPatch,sContenu,sEtag)
      			
      			SI rest_Reponse..CodeEtat DANS (200,201,202,204) ALORS
      				// modification task ok !
      				bRetour = Vrai
      				
      			SINON
      				bRetour = Faux
      			FIN
      		SINON
      			bRetour = faux
      		FIN
      		
      	SINON
      		bRetour = Faux
      	FIN
      SINON
      	bRetour = Faux
      FIN
      
      
      
      
      RENVOYER bRetour
     type : 458752
   -
     name : PLANNER_Modif_Etiquette
     procedure_id : 1576040817806432190
     type_code : 15
     code : |1+
      // Résumé : <indiquez ici ce que fait la procédure>
      // Syntaxe :
      //[ <Résultat> = ] PLANNER_Modif_Etiquette (<sIDPlan_p> est chaîne, <aStEtiquette_p> est tableau (STR_PLANNER_ETIQUETTE))
      //
      // Paramètres :
      //	sIDPlan_p (chaîne ANSI) : <indiquez ici le rôle de sIDPlan_p>
      //	aStEtiquette_p (tableau (STR_PLANNER_ETIQUETTE)) : <indiquez ici le rôle de stAstEtiquette_p>
      
      // Valeur de retour :
      // 	booléen : <indiquez ici le rôle de la valeur de retour>
      //
      // Exemple :
      // <Indiquez ici un exemple d'utilisation>
      //
      procédure PLANNER_Modif_Etiquette(LOCAL sIDPlan_p est une chaine,aStEtiquette_p est un tableau de STR_PLANNER_ETIQUETTE)<métier>:booleen
      
      // variables
      rest_Reponse	est un restRéponse
      bRetour est un booléen
      sContenu est une chaine
      sCategory est une chaine
      sEtag est une chaine
      vRetour est un variant
      
      
      POUR i = 1 _À_ aStEtiquette_p..Occurrence
      	sCategory += [RC] + [
      	"[%aStEtiquette_p[i].sIDCategory%]": "[%aStEtiquette_p[i].sNom_Category%]",
      	]
      FIN
      
      sContenu = [
      {
      	"categoryDescriptions": {
      		[%sCategory%]
      	}
      }
      ]
      
      // recupération Etag
      rest_Reponse	= COL_PLANNER.PLANNER_Construction_Execution_Requete("https://graph.microsoft.com/v1.0/planner/plans/[%sIDPlan_p%]/details",httpGet)
      
      SI rest_Reponse..CodeEtat DANS (200,201,202) ALORS
      	// requête ok
      	vRetour = JSONVersVariant(rest_Reponse..Contenu)
      	
      	sEtag = vRetour."@odata.etag"
      SINON
      	// requête hs
      	bRetour = Faux
      FIN
      
      // modif etiquette
      rest_Reponse	= COL_PLANNER.PLANNER_Construction_Execution_Requete("https://graph.microsoft.com/v1.0/planner/plans/[%sIDPlan_p%]/details",httpPatch,sContenu,sEtag)
      
      SI rest_Reponse..CodeEtat DANS (200,201,202,204) ALORS
      	// requête ok
      	bRetour = vrai
      SINON
      	// requête hs
      	bRetour = Faux
      FIN
      
      RENVOYER bRetour
     type : 458752
   -
     name : PLANNER_Récupération_Liste_Planner_Sans_Les_Taches
     procedure_id : 1576413487675132706
     type_code : 15
     code : |1+
      procédure PLANNER_Récupération_Liste_Planner_Sans_Les_Taches(LOCAL sNomGroupe_p est une chaîne)<metier>:tableau associatif de STR_PLANNER
      
      // variables
      rest_Reponse	est un restRéponse
      sIDGroupe		est une chaine
      vRetour			est un variant
      aStPlanner		est un tableau associatif de STR_PLANNER
      stPlanner		est un STR_PLANNER
      
      // récupération ID Groupes
      sIDGroupe		= COL_PLANNER.PLANNER_ID_GROUPES(sNomGroupe_p)
      
      // récupération des plans du groupe
      rest_Reponse	= COL_PLANNER.PLANNER_Construction_Execution_Requete("https://graph.microsoft.com/v1.0/groups/[%sIDGroupe%]/planner/plans",httpGet)
      
      SI rest_Reponse..CodeEtat DANS (200,201,202) ALORS
      	// requête ok
      	vRetour = JSONVersVariant(rest_Reponse..Contenu)
      	POUR nLigne = 1 _À_ TableauOccurrence(vRetour.value)
      		stPlanner.sNom_Planer	= vRetour.value[nLigne].title
      		stPlanner.sIDPlanner	= vRetour.value[nLigne].id
      		stPlanner.sIDGroupe		= vRetour.value[nLigne].owner
      		
      		SI PAS aStPlanner[stPlanner.sIDPlanner]..Existe ALORS
      			aStPlanner[stPlanner.sIDPlanner]	= allouer un STR_PLANNER
      			aStPlanner[stPlanner.sIDPlanner]	= stPlanner
      		FIN
      	FIN
      	
      	
      SINON
      	// requête hs
      	
      FIN
      
      RENVOYER aStPlanner
      
     type : 458752
   -
     name : PLANNER_Récupération_Plan_Complet
     procedure_id : 1576413977301453227
     type_code : 15
     code : |1+
      // Résumé : <indiquez ici ce que fait la procédure>
      // Syntaxe :
      //[ <Résultat> = ] PLANNER_Récupération_Plan_Complet (<sIDPlan_p>)
      //
      // Paramètres :
      //	sIDPlan_p : <indiquez ici le rôle de sIDPlan_p>
      
      // Valeur de retour :
      // 	tableau associatif (STR_PLANNER) : <indiquez ici le rôle de la valeur de retour>
      //
      // Exemple :
      // <Indiquez ici un exemple d'utilisation>
      //
      procédure PLANNER_Récupération_Plan_Complet(LOCAL sIDPlan_p)<métier>:STR_PLANNER
      
      // variables
      rest_Reponse	est un restRéponse
      vRetour			est un Variant
      stPlanner		est un STR_PLANNER
      
      
      // récupération des plans du groupe
      rest_Reponse	= COL_PLANNER.PLANNER_Construction_Execution_Requete("https://graph.microsoft.com/v1.0/planner/plans/[%sIDPlan_p%]",httpGet)
      
      SI rest_Reponse..CodeEtat DANS (200,201,202) ALORS
      	// requête ok
      	vRetour					= JSONVersVariant(rest_Reponse..Contenu)
      	
      	stPlanner.sNom_Planer	= vRetour.title
      	stPlanner.sIDPlanner	= vRetour.id
      	stPlanner.sIDGroupe		= vRetour.owner
      	
      	stPlanner.aStEtiquette	= COL_PLANNER.PLANNER_stListe_Etiquettes_Plan(stPlanner.sIDPlanner)
      	stPlanner.aStBucket		= COL_PLANNER.PLANNER_Récupération_Liste_Bucket_Taches(stPlanner)
      	
      SINON
      	// requête hs
      	
      FIN
      
      RENVOYER stPlanner
      
      
      
     type : 458752
   -
     name : PLANNER_Récupération_Plan_Par_ID
     procedure_id : 1576702358595368402
     type_code : 15
     code : |1+
      // Résumé : <indiquez ici ce que fait la procédure>
      // Syntaxe :
      //[ <Résultat> = ] PLANNER_Récupération_Plan_Par_ID (<sIDPlan_p>)
      //
      // Paramètres :
      // 	sIDPlan_p : <indiquez ici le rôle de sIDPlan_p>
      // Valeur de retour :
      // 	STR_PLANNER : <indiquez ici le rôle de la valeur de retour>
      //
      // Exemple :
      // <Indiquez ici un exemple d'utilisation>
      //
      procédure PLANNER_Récupération_Plan_Par_ID(LOCAL sIDPlan_p)<métier>:STR_PLANNER
      
      
      // variables
      rest_Reponse	est un restRéponse
      vRetour			est un Variant
      stPlanner		est un STR_PLANNER
      
      
      // récupération des plans du groupe
      rest_Reponse	= COL_PLANNER.PLANNER_Construction_Execution_Requete("https://graph.microsoft.com/v1.0/planner/plans/[%sIDPlan_p%]",httpGet)
      
      SI rest_Reponse..CodeEtat DANS (200,201,202) ALORS
      	// requête ok
      	vRetour					= JSONVersVariant(rest_Reponse..Contenu)
      	
      	stPlanner.sNom_Planer	= vRetour.title
      	stPlanner.sIDPlanner	= vRetour.id
      	stPlanner.sIDGroupe		= vRetour.owner
      	
      	
      SINON
      	// requête hs
      	
      FIN
      
      RENVOYER stPlanner
      
     type : 458752
   -
     name : PLANNER_Récupération_Nom_Groupe
     procedure_id : 1576703415157634504
     type_code : 15
     code : |1+
      // Résumé : <indiquez ici ce que fait la procédure>
      // Syntaxe :
      //[ <Résultat> = ] PLANNER_Récupération_Nom_Groupe (<sIDGroupe_p> est chaîne)
      //
      // Paramètres :
      //	sIDGroupe_p (chaîne ANSI) : <indiquez ici le rôle de sIDGroupe_p>
      // Valeur de retour :
      // 	chaîne ANSI : <indiquez ici le rôle de la valeur de retour>
      //
      // Exemple :
      // <Indiquez ici un exemple d'utilisation>
      //
      procédure PLANNER_Récupération_Nom_Groupe(LOCAL sIDGroupe_p est une chaîne)<métier>:chaîne
      
      sNomGroupe		est une chaîne
      rest_Reponse	est une restRéponse
      vRetour			est un Variant
      
      rest_Reponse = PLANNER_Construction_Execution_Requete("https://graph.microsoft.com/v1.0/groups/[%sIDGroupe_p%]",httpGet)
      
      SI rest_Reponse..CodeEtat DANS (200,201,202) ALORS
      	
      	vRetour = JSONVersVariant(rest_Reponse..Contenu)
      	
      	sNomGroupe = vRetour.displayName
      
      FIN
      
      RENVOYER sNomGroupe
      
     type : 458752
   -
     name : Encode_Chaine_JSON
     procedure_id : 1578174523149056166
     type_code : 15
     code : |1-
      // Résumé : <indiquez ici ce que fait la procédure>
      // Syntaxe :
      //[ <Résultat> = ] Encode_Chaine_JSON (<sChaine_p> est chaîne)
      //
      // Paramètres :
      //	sChaine_p (chaîne ANSI) : <indiquez ici le rôle de sChaine_p>
      // Valeur de retour :
      // 	chaîne ANSI : <indiquez ici le rôle de la valeur de retour>
      //
      // Exemple :
      // <Indiquez ici un exemple d'utilisation>
      //
      procédure Encode_Chaine_JSON(LOCAL sChaine_p est une chaine)
      
      sChaine_Retour est une chaine
      
      sChaine_Retour = sChaine_p
      
      // échappement dans sujet
      sChaine_Retour		= Remplace(sChaine_Retour,"\","\\")
      sChaine_Retour		= Remplace(sChaine_Retour,Caract(34),"\" + Caract(34))
      sChaine_Retour		= Remplace(sChaine_Retour,TAB,"\t")
      
      RENVOYER sChaine_Retour
     type : 458752
  procedure_templates : []
  property_templates : []
 code_parameters :
  internal_properties : BwAAAAcAAAA6ih3UbgNXHwTtiPSFUEj+2fi/m7v4QV2rqidAupM=
  original_name : COL_SansNom1
resources :
 string_res :
  identifier : 0x155db8ad002f7c44
  internal_properties : BwAAAAcAAAAnMYFQ1bL/vz9ehh7L22SNNSlIzGTOI8h5F/WtgDNP
custom_note :
 internal_properties : BwAAAAcAAABtB9HWVzrXO2+4NDRVK0vmzaNKrCKqH1DBX30lMmGZ
