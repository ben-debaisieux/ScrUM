#To edit and compare internal_properties, use WINDEV integrated tools.
#Internal properties refer to the properties of controls in windows, reports, etc.
info :
 name : ClZF_AuditModele
 major_version : 27
 minor_version : 0
 type : 4
 description : ""
 subtype : 0
class :
 identifier : 0x1483817c008aa4e5
 internal_properties : BwAAAAcAAAB2/vstTMCJbS/hlxjFxirSqKvCUuv8YxgpWyl7S3iA
 code_elements :
  type_code : 10
  p_codes :
   -
     code : |1+
      ClZF_AuditModele est une Classe
      
      PROTÉGÉ
      	m_oClasseGen	est un ClZF_GenCodeFic dynamique
      	m_sRapport		est une chaîne
      fin
     type : 131072
  procedures :
   -
     name : Constructeur
     procedure_id : 1478167472292734181
     type_code : 27
     code : |1-
      procédure Constructeur(sNomTable_p est une chaine)
      
      :m_oClasseGen = allouer un  ClZF_GenCodeFic(sNomTable_p)
      :m_oClasseGen.Analyse()
     type : 589824
   -
     name : Destructeur
     procedure_id : 1478167472292799717
     type_code : 28
     code : |1+
      procédure Destructeur()
     type : 655360
   -
     name : sRamureTbl
     procedure_id : 1478167979099321461
     type_code : 12
     code : |1+
      procedure sRamureTbl()<metier>:chaine
      renvoyer Répète("#", 100) + RC + :m_oClasseGen.p_sNomTable + RC + Répète("#", 100) + RC
     type : 458752
   -
     name : AnaPk
     procedure_id : 1478177952015573514
     type_code : 12
     code : |1+
      procedure AnaPk()<metier>:vide
      
      SI :m_oClasseGen.p_sNomRubId = "" ALORS
      	:m_sRapport	= "Aucune rubrique 'PK' n'est identifiée."
      FIN
     type : 458752
   -
     name : AnaFk
     procedure_id : 1478176161013859180
     type_code : 12
     code : |1+
      PROCÉDURE AnaFk()<métier>:Vide
      
      
      sRapport		est une chaîne
      stRelTmp		est un ClZF_GenCodeFic.str_rel
      sRub			est une chaîne
      nNbFkIncl		est un entier
      sListeFkCompo	est une chaîne
      stInfoRub		est un ClZF_GenCodeFic.STR_DefRub
      
      
      
      POUR TOUT ÉLÉMENT stRelTmp, sRub DE :m_oClasseGen.m_aPK_de_FK
      
      	//Récupération de la définition de la rubrique associé à la contrainte FK
      	si sRub <> :m_oClasseGen.p_sNomRubId alors
      		stInfoRub  = :m_oClasseGen.m_aRub[sRub]
      	sinon
      		stInfoRub  = :m_oClasseGen.p_stDefId
      	fin
      	
      	SELON stRelTmp.nTypeRel
      		CAS ClZF_GenCodeFic.cstRelErreur
      			traceInfo("Liaison ""[%stRelTmp.sNomRel%]"" aberrante ([%stRelTmp.sTablePK%].[%stRelTmp.sRubriquePK%] -> [%stRelTmp.sTableFK%].[%stRelTmp.sRubriqueFK%]).")
      		CAS ClZF_GenCodeFic.cstRelNonDef
      			traceInfo("Liaison ""[%stRelTmp.sNomRel%]"" non normalisée ([%stRelTmp.sTablePK%].[%stRelTmp.sRubriquePK%] -> [%stRelTmp.sTableFK%].[%stRelTmp.sRubriqueFK%]).")
      		CAS ClZF_GenCodeFic.cstRelIncl
      			nNbFkIncl++
      			sListeFkCompo += [RC] + stRelTmp.sNomRel
      			
      			si stInfoRub.nTypeCle <> ClZF_GenCodeFic.cstTypeCleUnique ALORS
      				traceInfo("Liaison ""[%stRelTmp.sNomRel%]"", de type ""Inclusion"", pointe vers une rubrique non unique : [%sRub%].")
      			FIN
      		autres cas :
      			//Pas d'autres analyses particulières.
      	FIN
      	
      	SI stInfoRub.bNullable ET stRelTmp.bRubFKOblig ALORS
      		traceInfo("Liaison ""[%stRelTmp.sNomRel%]"" incohérente : ([%sRub%] nullable et relation de type ""1,1"".")
      	FIN
      	SI PAS stInfoRub.bNullable ET PAS stRelTmp.bRubFKOblig ALORS
      		traceInfo("Liaison ""[%stRelTmp.sNomRel%]"" incohérente : ([%sRub%] non nullable et relation de type ""0,1"".")
      	FIN
      	
      	
      	
      	SI stInfoRub.nTypeCle = ClZF_GenCodeFic.cstTypeCleUnique ET pas stRelTmp.bRubFKUnique ALORS
      		traceInfo("Liaison ""[%stRelTmp.sNomRel%]"" incohérente : [%sRub%] clé unique et relation de type ""0,n"" ou ""1,n"".")
      	FIN
      	SI stInfoRub.nTypeCle <> ClZF_GenCodeFic.cstTypeCleUnique ET stRelTmp.bRubFKUnique ALORS
      		traceInfo("Liaison ""[%stRelTmp.sNomRel%]"" incohérente : [%sRub%] non clé unique et relation de type ""0,1"" ou ""1,1"".")
      	FIN
      	
      FIN
      
      
      
      SI nNbFkIncl > 1 ALORS
      	traceInfo("[%nNbFkIncl%] liaisons sont de type ""Inclusion"", dans cette table (1 seule maxi): " + RC + sListeFkCompo)
      FIN
      
      
      :m_sRapport = sRapport
      
      
      PROCÉDURE INTERNE traceInfo(sMessage):vide
      	sRapport += [RC] + sMessage
      FIN
      
     type : 458752
   -
     name : AnaLibRubs
     procedure_id : 1479647406539584332
     type_code : 12
     code : |1+
      PROCÉDURE AnaLibRubs()<métier>:Vide
      
      
      sRapport		est une chaîne
      stRubTmp		est un ClZF_GenCodeFic.STR_DefRub
      sRub			est une chaîne
      
      
      POUR TOUT ÉLÉMENT stRubTmp, sRub DE :m_oClasseGen.m_aRub
      	//SI le libellé et le nom de la rubrique sont identiques, c'est que le libellé n'a pas été redéfinit
      	si sRub = stRubTmp.sLibelleRubrique ALORS
      		traceInfo("Le libellé de la rubrique ""[%sRub%]"" n'a pas été redéfini.")
      	FIN
      FIN
      
      
      :m_sRapport = sRapport
      
      
      PROCÉDURE INTERNE traceInfo(sMessage):vide
      	sRapport += [RC] + sMessage
      FIN
      
      
     type : 458752
   -
     name : AnaRubsNull
     procedure_id : 1549960337520702726
     type_code : 12
     code : |1+
      PROCÉDURE AnaRubsNull()<métier>:Vide
      
      
      sRapport		est une chaîne
      stRubTmp		est un ClZF_GenCodeFic.STR_DefRub
      sRub			est une chaîne
      
      
      POUR TOUT ÉLÉMENT stRubTmp, sRub DE :m_oClasseGen.m_aRub
      	//Si la rubique est nullable 
      	SI stRubTmp.bNullable ALORS
      		
      		//et n'est pas une clé étrngère (cas normal)
      		si :m_oClasseGen.m_aPK_de_FK[sRub]..Vide alors
      			traceInfo("Vérifier s'il est pertinent que la rubrique ""[%sRub%]"" soit nullable.")
      		FIN
      		
      		si pas stRubTmp.bNullDefaut ALORS
      			traceInfo("La rubrique ""[%sRub%]"" est nullable, mais n'est pas a null par défaut.")
      		FIN
      	FIN
      FIN
      
      
      :m_sRapport = sRapport
      
      
      	PROCÉDURE INTERNE traceInfo(sMessage):Vide
      		sRapport += [RC] + sMessage
      	FIN
      
     type : 458752
   -
     name : AnaPropriete
     procedure_id : 1478171939059827917
     type_code : 12
     code : |1+
      procedure AnaPropriete()<metier>:vide
      
      
      
      str_compareObj est une Structure
      	bPresAnalyse	est un booléen
      	bMembreMap		est un booléen
      	bPropLect		est un booléen
      	bPropEcr		est un booléen
      	bMembreObj		est un booléen
      	bPropIdLect		est un booléen
      	bPropIdEcr		est un booléen
      	sNomMembre		est une chaîne
      FIN
      
      
      sRapport est une chaîne
      
      SI :m_oClasseGen.p_bClasseExiste ALORS
      	
      	//-------------------------------------
      	//
      	
      	oTmp est un objet dynamique
      	
      	quand exception dans
      		oTmp = ClZF_ObjDbLs.ZF_GetObj(:m_oClasseGen.sNomClasseGen(), ClZF_ObjDbLs.cstModeSansChargement)
      	faire
      		sRapport = [
      			Analyse impossible des propriétés de la classe : [%:m_oClasseGen.sNomClasseGen()%] :
      			[%ExceptionInfo(errMessage)%]
      			]
      	fin
      //	SI PAS oTmp est ClZF_ObjDbLs ALORS
      //		ExceptionDéclenche(1,"Type incompatible")
      //		RETOUR
      //	FIN
      	
      	
      	oObjBD est un ClZF_GenCodeClBd(:m_oClasseGen)
      	
      	SI oTmp est ClZF_ObjDB ALORS
      		oObjBD.p_bLectureSeule = Faux
      	SINON
      		oObjBD.p_bLectureSeule = Vrai
      	FIN
      	
      	
      	
      	//-------------------------------------
      	//récupération des rubriques attendues.
      	sRub			est une chaîne
      	stRubAnaTmp		est un ClZF_GenCodeFic.str_DefRub
      	stInfosDiffRub	est un tableau associatif (ccSansCasse + ccSansEspace) de str_compareObj
      	stDiffRub		est un str_compareObj
      	
      	POUR TOUT ÉLÉMENT stRubAnaTmp, sRub DE :m_oClasseGen.m_aRub
      		VariableRAZ(stDiffRub)
      		stDiffRub.bPresAnalyse		= Vrai
      		stDiffRub.bMembreObj		= (stRubAnaTmp.oObj_Parent <> null)
      		stDiffRub.sNomMembre		= stRubAnaTmp.sNomMembre
      		stInfosDiffRub[sRub]		= stDiffRub
      	FIN
      	
      	
      	//-------------------------------------------------
      	//Récupération des membres déclarées dans la classe
      	Vars			est une Description de variable
      	Def				est une Définition	= RécupèreDéfinitionType(:m_oClasseGen.sNomClasseGen())
      	
      	POUR TOUT Vars DE Def.Variable
      		
      		sRub = ""
      		SI Vars.AttributMapping <> "" ALORS
      			sRub = Vars.AttributMapping
      		FIN
      		SI Vars.AttributNote <> "" ALORS
      			aNotes est un tableau associatif de chaînes = col_zf_surcharge.chaineverstableauasso(Vars.AttributNote)
      			sRub = aNotes["mapping"]
      		FIN
      		
      		
      		SI sRub <> "" ALORS
      			SI stInfosDiffRub[sRub]..Vide ALORS
      				stInfosDiffRub[sRub] = allouer un str_compareObj
      			FIN
      			stDiffRub				= stInfosDiffRub[sRub]
      			
      			stDiffRub.bMembreMap	= Vrai
      			stDiffRub.sNomMembre	= Vars.Nom
      			
      			stInfosDiffRub[sRub]	= stDiffRub
      		FIN
      	FIN
      	
      	
      	//------------------------------------------------
      	//Analyse des propriétés de la classe sélectionnée
      	Prop	est une Description de Propriété
      	sMembre	est une chaîne
      	POUR TOUT Prop DE Def.Propriété  
      		SI PAS Prop.Hérité ALORS
      			sMembre		= Prop.Nom
      			sMembre[1]	= "m"
      			
      			//Parcout des rubriques attendues :
      			POUR TOUT ÉLÉMENT stDiffRub, sRub DE stInfosDiffRub
      				
      				SI sMembre ~= stDiffRub.sNomMembre ALORS
      					stDiffRub.bPropLect		= Prop.Lecture
      					stDiffRub.bPropEcr		= Prop.Ecriture OU oObjBD.p_bLectureSeule
      				FIN
      				
      				SI stDiffRub.bMembreObj ALORS
      					//				trace(sMembre + "_ID" + ">" + stDiffRub.sNomMembre)
      					SI sMembre ~= stDiffRub.sNomMembre + "_ID" ALORS
      						stDiffRub.bPropIdLect	= Prop.Lecture
      						stDiffRub.bPropIdEcr	= Prop.Ecriture OU oObjBD.p_bLectureSeule
      					FIN
      				FIN
      			FIN
      		FIN
      	FIN
      	
      	
      	
      	
      	POUR TOUT ÉLÉMENT stDiffRub, sRub DE stInfosDiffRub
      		
      		
      		SI PAS stDiffRub.bMembreMap ALORS
      			sRapport += [RC + RC] + "Rubrique ""[%sRub%]"" non mappée dans la classe [%:m_oClasseGen.sNomClasseGen()%]."
      			
      			blocMapping(m_oClasseGen.m_aRub[sRub], sRub)
      		FIN
      		
      		SI PAS stDiffRub.bPresAnalyse ALORS
      			sRapport += [RC + RC] + "Rubrique ""[%sRub%]"" mappée dans la classe ""[%:m_oClasseGen.sNomClasseGen()%]"", mais non présente dans la table ""[%:m_oClasseGen.p_sNomTable%]""."
      		FIN
      		
      		SI stDiffRub.bPresAnalyse ET stDiffRub.bMembreMap ALORS
      			
      			SI PAS stDiffRub.bPropLect ET PAS stDiffRub.bPropEcr ALORS
      				sRapport += [RC + RC] + "Accesseur en lecture / écriture manquant pour le membre ""[%stDiffRub.sNomMembre%]""."
      				blocPropriete(oObjBD, sRub)
      			SINON
      				SI PAS stDiffRub.bPropLect ALORS
      					sRapport += [RC + RC] + "Accesseur en lecture manquant pour le membre ""[%stDiffRub.sNomMembre%]""."
      					blocPropriete(oObjBD, sRub)
      				FIN
      				
      				SI PAS stDiffRub.bPropEcr ALORS
      					sRapport += [RC + RC] + "Accesseur en écriture manquant pour le membre ""[%stDiffRub.sNomMembre%]""."
      					blocPropriete(oObjBD, sRub)
      				FIN
      			FIN
      			
      			SI stDiffRub.bMembreObj ALORS
      				SI PAS stDiffRub.bPropIdLect ET PAS stDiffRub.bPropIdEcr ...		//s'il manque les propriétés des ids
      						et (stDiffRub.bPropLect ou stDiffRub.bPropEcr) ALORS		//mais qu'il y a les propriétés sur l'objet	(sinon, déja traitée plus haut)
      					sRapport += [RC + RC] + "Accesseur en lecture / écriture manquant pour le membre objet ""[%stDiffRub.sNomMembre%]"" via son ID."
      					blocProprieteId(oObjBD, sRub)
      				SINON
      					SI PAS stDiffRub.bPropIdLect ...								//S'il manque la propriété de l'ID en lecture
      							ET stDiffRub.bPropLect ALORS							//mais qu'il y a la propriété sur l'objet
      						sRapport += [RC + RC] + "Accesseur en lecture manquant pour le membre objet ""[%stDiffRub.sNomMembre%]"" via son ID."
      						blocProprieteId(oObjBD, sRub)
      					FIN
      					
      					SI PAS stDiffRub.bPropIdEcr ...									//S'il manque la propriété de l'ID en écriture
      							ET stDiffRub.bPropEcr ALORS								//mais qu'il y a la propriété sur l'objet
      						sRapport += [RC + RC] + "Accesseur en écriture manquant pour le membre objet ""[%stDiffRub.sNomMembre%]"" via son ID."
      						blocProprieteId(oObjBD, sRub)
      					FIN
      				FIN
      			FIN
      		FIN
      	FIN
      sinon
      	sRapport += [RC + RC] + "La classe [%:m_oClasseGen.sNomClasseGen()%] n'existe pas."
      FIN
      
      :m_sRapport = sRapport
      
      
      
      
      	PROCÉDURE INTERNE blocPropriete(oObjBD_p est un ClZF_GenCodeClBd, sRub_p est une chaine):vide
      		sRapport += RC + :sSepCode()
      		sRapport += RC + oObjBD_p.GenPropriete(sRub_p)
      		sRapport += RC + :sSepCode()
      	FIN
      
      	PROCÉDURE INTERNE blocProprieteId(oObjBD_p est un ClZF_GenCodeClBd, sRub_p est une chaine):vide
      		sRapport += RC + :sSepCode()
      		sRapport += RC + oObjBD_p.GenProprieteId(sRub_p)
      		sRapport += RC + :sSepCode()
      	FIN
      
      	PROCÉDURE INTERNE blocMapping(stDefRub_p est un ClZF_GenCodeFic.STR_DefRub, sRub_p est une chaîne):vide
      		sRapport += RC + :sSepCode()
      		sRapport += RC + ClZF_GenCodeClBd::sDeclareMembre(stDefRub_p, sRub_p)
      		sRapport += RC + :sSepCode()
      	FIN
      
     type : 458752
   -
     name : sSepCode
     procedure_id : 1556374308591352724
     type_code : 12
     code : |1+
      PROCÉDURE prive sSepCode()<metier>:chaîne
      RENVOYER Répète("-",150)
     type : 458752
   -
     name : AnaMeta
     procedure_id : 1478173480953512014
     type_code : 12
     code : |1+
      pROCÉDURE AnaMeta()<métier>:Vide
      
      
      
      str_compareMeth est une Structure
      	bPresAnalyse	est un booléen
      	bPresObj		est un booléen
      	sCodeCrea		est une chaîne
      FIN
      
      
      
      
      sRapport est une chaîne
      
      SI :m_oClasseGen.p_bClasseExiste ALORS
      	
      	
      	stRelTmp		est un ClZF_GenCodeFic.STR_Rel
      	stDiffRub		est un str_compareMeth
      	sRub			est une chaîne
      	stInfosDiffRub	est un tableau associatif (ccSansCasse + ccSansEspace) de str_compareMeth
      	
      	
      	
      	//------------------------------------
      	//récupération des méthodes attendues.	
      	POUR TOUT ÉLÉMENT stRelTmp DE :m_oClasseGen.m_aPK_de_FK
      		SI stRelTmp.nTypeRel DANS(ClZF_GenCodeFic.cstRelAsso, ClZF_GenCodeFic.cstRelCompo) ALORS
      			
      			clOClGen est un ClZF_GenCodeFic(stRelTmp.sTablePK)
      			sRub = ClZF_GenCodeClCol.sNomFct_ChargeFrom(clOClGen)
      			
      			
      			oGenCol est un ClZF_GenCodeClCol(clOClGen)
      			oGenCol.SetNatif = Faux
      			
      			VariableRAZ(stDiffRub)
      			stDiffRub.sCodeCrea			= oGenCol.GenProc_ChargeFromDeRel(stRelTmp)
      			stDiffRub.bPresAnalyse		= Vrai
      			stInfosDiffRub[sRub]		= stDiffRub
      			
      		FIN
      	FIN
      	
      	
      	
      	//--------------------------------------------------
      	//Récupération des méthodes déclarées dans la classe
      	Vars			est une Description de procédure
      	Def				est une Définition	= RécupèreDéfinitionType(:m_oClasseGen.sNomClasseColGen())
      	
      	SI Def <> Null ALORS
      		POUR TOUT Vars DE Def.Procédure
      			sRub = Vars.Nom
      			SI stInfosDiffRub[sRub]..Vide ALORS
      				stInfosDiffRub[sRub] = allouer un str_compareMeth
      				VariableRAZ(stDiffRub)
      				stDiffRub.bPresAnalyse = Faux
      			SINON
      				stDiffRub = stInfosDiffRub[sRub]
      			FIN
      			stDiffRub.bPresObj			= Vrai
      			stInfosDiffRub[sRub]		= stDiffRub
      		FIN
      		
      		
      		POUR TOUT ÉLÉMENT stDiffRub, sRub DE stInfosDiffRub
      			SI stDiffRub.bPresAnalyse ET PAS stDiffRub.bPresObj ALORS
      				sRapport += [RC + RC] + "Méthode ""[%sRub%]"" non présente dans la classe [%:m_oClasseGen.sNomClasseColGen()%]."
      				blocCode(stDiffRub.sCodeCrea)
      			FIN
      		FIN
      		
      	SINON
      		sRapport += [RC + RC] + "Classe ""[%:m_oClasseGen.sNomClasseColGen()%]"" non présente."
      	FIN
      FIN
      
      
      :m_sRapport = sRapport
      
      
      
      PROCÉDURE INTERNE blocCode(sMsg)
      	sRapport += RC + :sSepCode()
      	sRapport += RC + sMsg
      	sRapport += RC + :sSepCode()
      FIN
      
     type : 458752
   -
     name : AnaColMqt
     procedure_id : 1478175220415671222
     type_code : 12
     code : |1-
      PROCÉDURE AnaColMqt()<métier>:Vide
      
      
      
      
      sRapport est une chaîne
      
      
      SI :m_oClasseGen.p_bClasseExiste ALORS
      	
      	
      	stRelTmp			est un ClZF_GenCodeFic.STR_Rel
      	sConstRef			est une chaîne
      	sCodeCrea			est une chaîne
      	tabACleMqt			est un tableau de chaine
      	
      	//------------------------------------
      	//récupération des méthodes attendues.	
      	POUR TOUT ÉLÉMENT stRelTmp DE :m_oClasseGen.m_aPK_de_FK
      		SI stRelTmp.nTypeRel DANS (ClZF_GenCodeFic.cstRelCompo) ALORS
      			
      			sConstRef	= ClZF_GenCodeClCol.sNomConstanteColCompo(stRelTmp,<bValeurConstante_p>:vrai)
      			//sConstRef	= """" + stRelTmp.sTableFK + "." + stRelTmp.sRubriqueFK + """"		//recréation de la valeur de la constant, car ExécuteCode bugge
      			
      			QUAND EXCEPTION DANS
      				sCode est une chaîne = ChaîneConstruit([
      					clOCol%1 est un [%:m_oClasseGen.sNomClasseColGen()%]()
      					clODb%1 est un [%:m_oClasseGen.sNomClasseGen()%](ClZF_ObjDbLs.cstModeCreation)
      					clOCol%1 <- clODb%1.ZF_GetCol("[%sConstRef%]")
      					libérer clODb%1
      					libérer clOCol%1
      				], DonneIdentifiant())
      				ExécuteCode(sCode)
      			FAIRE
      				SELON ExceptionInfo(errCode)
      					CAS ClZF_App.mg_oExTypeColNonDefinit.ZF_Code
      						TableauAjoute(tabACleMqt, ClZF_GenCodeClCol.sNomConstanteColCompo(stRelTmp,Vrai))
      					AUTRE CAS
      						ExceptionPropage()
      				FIN
      				
      			FIN
      		FIN
      	FIN
      	
      	
      	SI TableauOccurrence(tabACleMqt) > 0 ALORS
      		
      		sCodeCrea = [
      			CAS [%TableauVersChaîne(tabACleMqt, ", ")%] :
      			[%TAB%]oCol = allouer un [%:m_oClasseGen.sNomClasseColGen()%]
      		]
      		
      		sRapport += [RC + RC] + "Code de déclaration des collections manquant (procédure CreeCol)."
      		blocCode(sCodeCrea)
      		
      	FIN
      FIN
      
      
      :m_sRapport = sRapport
      
      
      
      	PROCÉDURE INTERNE blocCode(sMsg)
      		sRapport += RC + :sSepCode()
      		sRapport += RC + sMsg
      		sRapport += RC + :sSepCode()
      	FIN
     type : 458752
   -
     name : bMsgInfo
     procedure_id : 1478180915544287250
     type_code : 12
     code : |1+
      // Résumé : <indiquez ici ce que fait la procédure>
      // Syntaxe :
      //[ <Résultat> = ] bMsgInfo ()
      //
      // Paramètres :
      //	Aucun
      // Valeur de retour :
      // 	booléen : <indiquez ici le rôle de la valeur de retour>
      //
      // Exemple :
      // <Indiquez ici un exemple d'utilisation>
      //
      procédure bMsgInfo() <métier>:booleen
      renvoyer :m_sRapport <> ""
      
     type : 458752
   -
     name : AnaFkMqt
     procedure_id : 1569839189853034151
     type_code : 12
     code : |1+
      PROCÉDURE AnaFkMqt()<métier>:Vide
      
      
      sRapport		est une chaîne
      stRubTmp		est un ClZF_GenCodeFic.STR_DefRub
      //stRelTmp		est un ClZF_GenCodeFic.str_rel
      sRub			est une chaîne
      //nNbFkIncl		est un entier
      //sListeFkCompo	est une chaîne
      //stInfoRub		est un ClZF_GenCodeFic.STR_DefRub
      
      
      //sNomPK			est une chaîne	= :m_oClasseGen.p_sNomRubId
      oTblTmp			est un ClZF_GenCodeFic dynamique
      sNomTblTmp		est une chaine
      
      pour toute chaine sNomTblTmp de ClZF_GenCodeFic.sListeFicAna() separee par RC
      	oTblTmp = allouer un ClZF_GenCodeFic(sNomTblTmp)
      	oTblTmp.Analyse()
      	POUR TOUT ÉLÉMENT stRubTmp, sRub DE m_oClasseGen.m_aRub
      		//trace(oTblTmp.p_sNomRubId + " -" + sRub)
      		si ChaîneCommencePar(sRub, oTblTmp.p_sNomRubId) _et_ stRubTmp.stRelFkAsso.sTablePK <> sNomTblTmp alors
      			
      			traceInfo([
      				La rubrique [%srub%] ne référence pas la clé primaire [%sNomTblTmp%].[%oTblTmp.p_sNomRubId%], alors qu'elles ont le même nom.
      				])
      			
      		FIN
      	FIN
      FIN
      
      
      //POUR TOUT ÉLÉMENT stRelTmp, sRub DE :m_oClasseGen.m_aPK_de_FK
      //
      //	//Récupération de la définition de la rubrique associé à la contrainte FK
      //	si sRub <> :m_oClasseGen.p_sNomRubId alors
      //		stInfoRub  = :m_oClasseGen.m_aRub[sRub]
      //	sinon
      //		stInfoRub  = :m_oClasseGen.p_stDefId
      //	fin
      //	
      //	SELON stRelTmp.nTypeRel
      //		CAS ClZF_GenCodeFic.cstRelNonDef
      //			traceInfo("Liaison ""[%stRelTmp.sNomRel%]"" non normalisée ([%stRelTmp.sTablePK%].[%stRelTmp.sRubriquePK%] -> [%stRelTmp.sTableFK%].[%stRelTmp.sRubriqueFK%]).")
      //		CAS ClZF_GenCodeFic.cstRelIncl
      //			nNbFkIncl++
      //			sListeFkCompo += [RC] + stRelTmp.sNomRel
      //			
      //			si stInfoRub.nTypeCle <> ClZF_GenCodeFic.cstTypeCleUnique ALORS
      //				traceInfo("Liaison ""[%stRelTmp.sNomRel%]"", de type ""Inclusion"", pointe vers une rubrique non unique : [%sRub%].")
      //			FIN
      //		autres cas :
      //			//Pas d'autres analyses particulières.
      //	FIN
      //	
      //	SI stInfoRub.bNullable ET stRelTmp.bRubFKOblig ALORS
      //		traceInfo("Liaison ""[%stRelTmp.sNomRel%]"" incohérente : ([%sRub%] nullable et relation de type ""1,1"".")
      //	FIN
      //	SI PAS stInfoRub.bNullable ET PAS stRelTmp.bRubFKOblig ALORS
      //		traceInfo("Liaison ""[%stRelTmp.sNomRel%]"" incohérente : ([%sRub%] non nullable et relation de type ""0,1"".")
      //	FIN
      //	
      //	
      //	
      //	SI stInfoRub.nTypeCle = ClZF_GenCodeFic.cstTypeCleUnique ET pas stRelTmp.bRubFKUnique ALORS
      //		traceInfo("Liaison ""[%stRelTmp.sNomRel%]"" incohérente : [%sRub%] clé unique et relation de type ""0,n"" ou ""1,n"".")
      //	FIN
      //	SI stInfoRub.nTypeCle <> ClZF_GenCodeFic.cstTypeCleUnique ET stRelTmp.bRubFKUnique ALORS
      //		traceInfo("Liaison ""[%stRelTmp.sNomRel%]"" incohérente : [%sRub%] non clé unique et relation de type ""0,1"" ou ""1,1"".")
      //	FIN
      //	
      //FIN
      //
      //
      //
      //SI nNbFkIncl > 1 ALORS
      //	traceInfo("[%nNbFkIncl%] liaisons sont de type ""Inclusion"", dans cette table (1 seule maxi): " + RC + sListeFkCompo)
      //FIN
      
      
      :m_sRapport = sRapport
      
      
      PROCÉDURE INTERNE traceInfo(sMessage):vide
      	sRapport += [RC] + sMessage
      FIN
      
     type : 458752
  properties :
   -
     name : p_sRapport
     identifier : 0x1483821a00940f4e
     type_code : 103
     p_codes :
      -
        code : |1-
         procédure publique p_sRapport() : chaîne
         renvoyer m_sRapport
        type : 1966080
     template_refs : []
  procedure_templates : []
  property_templates : []
 code_parameters :
  internal_properties : BwAAAAcAAAA6ih3UbgNXHwTtiPSFUEj+2fi/m7v4QV2rqidAupM=
  original_name : Classe1
resources :
 string_res :
  identifier : 0x1483815e0084313c
  internal_properties : BwAAAAcAAAAnMYFQ1bL/vz9ehh7L22SNNSlIzGTOI8h5F/WtgDNP
custom_note :
 internal_properties : BwAAAAcAAABtB9HWVzrXO2+4NDRVK0vmzaNKrCKqH1DBX30lMmGZ
